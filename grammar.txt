
program         -> declaration* EOF;

declaration     -> var_decl | statement;

var_decl        -> "var" IDENTIFIER ("=" expression)? ";";

statement       -> print_stmt | block | ifstmt | expression_stmt | while_stmt;

print_stmt      -> "print" expression ";";

expression_stmt -> expression ";";

block           -> "{" declaration* "}";

ifstmt          -> "if" "(" expression ")" statement ("else" statement)?;

while_stmt      -> "while" "(" expression ")" statement;

expression      -> assignment;

assignment      -> IDENTIFIER "=" assignment | logic_or;

logic_or        -> logic_and ( "or" logic_and )*;

logic_and       -> equality ( "and" equality )*;

equality        -> comparison ( ( "!=" | "==" ) comparison )* ;

comparison      -> expr ( ( ">" | ">=" | "<" | "<=" ) expr )* ;

expr            -> term (("+"|"-") term)*;

term            -> factor (("*"|"/") factor)*;

factor          -> "-" factor | "!" factor | NUMBER | STRING | "(" expression ")" | IDENTIFIER | "nil" | "true" | "false";