
program         -> declaration* EOF;

declaration     -> fun_decl | var_decl | statement;

fun_decl        -> "fun" function;

function        -> IDENTIFIER "(" parameters? ")" block;

parameters      -> IDENTIFIER ( "," IDENTIFIER )*;

var_decl        -> "var" IDENTIFIER ("=" expression)? ";";

statement       -> print_stmt | block | ifstmt | expression_stmt | while_stmt | for_stmt;

print_stmt      -> "print" expression ";";

expression_stmt -> expression ";";

block           -> "{" declaration* "}";

ifstmt          -> "if" "(" expression ")" statement ("else" statement)?;

while_stmt      -> "while" "(" expression ")" statement;

for_stmt        -> "for" "(" ( var_decl | expression_stmt | ";" ) expression? ";" expression? ")" statement;

expression      -> assignment;

assignment      -> IDENTIFIER "=" assignment | logic_or;

logic_or        -> logic_and ( "or" logic_and )*;

logic_and       -> equality ( "and" equality )*;

equality        -> comparison ( ( "!=" | "==" ) comparison )* ;

comparison      -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;

term            -> factor (("+"|"-") factor)*;

factor          -> unary (("*"|"/") unary)*;

unary           -> ( "!" | "-" ) unary | call ;

call            -> primary ( "(" arguments? ")" )* ;

arguments       -> expression ( "," expression )* ;

primary         -> "true" | "false" | "nil" | NUMBER | STRING | IDENTIFIER | "(" expression ")";